/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */
options
{
 static = false;
}

PARSER_BEGIN(Parser)
package parser;
import data.*;
import java.util.*;
import validations.*;
import exceptions.*;
import utils.*;
import types.*;
import ast.*;

public class Parser
{}

PARSER_END(Parser)

<*> SKIP :
{
	< " " | "\t" | "\r" | "\n" | "\r\n" >
	| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n") >
	| <"/*"(~["/"])*  "*""/" >
}

// Types
TOKEN :
{
  	< INT: "int" >
  	| < BOOL: "bool" >
 	| < STRING: "string">
 	| < SET: "set">
 	// | < PTYPE: "PType">
}

TOKEN :
{
  	< PLUS : "+" >
  	| < MINUS : "-">
  	| < TIMES : "*">
  	| < DIV : "/">
}

// Logical Operators :
TOKEN :
{
  	< AND : "AND" | "&" >
  	| < NOT: "NOT" >
	| < OR : "OR" >
}

//Comparison operators
TOKEN :
{
  	< EQ : "==" >
	| < NEQ : "!=" | "<>" >
	| < LEQ : "<=" >
	| < GEQ: ">=" >
	| < LT : "<" >
	| < GT : ">" >
}

// Boolean Values
TOKEN :
{
  	< TRUE : "true" | "TRUE" >
  	| < FALSE : "false" | "FALSE" > 
}

// Constants
TOKEN :
{
	< STARTLABEL : "starts" | "STARTS" | "deploy" | "DEPLOY" >
	| < CONTAINS : "in" > 
}

// Brackets
TOKEN :
{
  	< LBRACKET : "{" >
	| < RBRACKET : "}" >
	| < LPAR : "(" >
  	| < RPAR : ")" >
  	| < SLPAR : "[" >
  	| < SRPAR : "]" >
  	| < QUOTE : "\"" >
}

TOKEN :
{
	< UNDERSCORE : "_" >
 	| < COLON : ":" >
 	| < SEMICOLON : ";" >
 	| < COMMA: "," >
  	| < VAR : "var" >
  	| < VARASS : ":=" >
 	| < MOREPART : "|" > 
	| < DOT : ["."]  >
}


TOKEN :
{	< EXISTS:  "exists" | "EXISTS" >
	// "âˆ€"
	| < FORALL: "forall" | "FORALL" >
  	| < NUMBER : (["0"-"9"])+ >
 	| < #CHAR : ["A"-"Z","a"-"z"] >
	| < #ID : (["A"-"Z","a"-"z"])+ >
	| < IDWN : (<CHAR> | "_") (<ID> | <NUMBER>)* >
}

TOKEN :
{
  < STRING_LITERAL : ["\""]  (~["\n","\r"])*  ["\""]>
}

int Start(Hashtable<String, Automaton> auto, ValidationChecks checks) throws CustomException, TypingException :
{}
{
  	DeployTransition(auto,checks) {	return 0; }
 	| NormalTransition(auto, checks) { return 0; }
   	| <EOF>	{ return 1;	}
}

void DeployTransition(Hashtable<String, Automaton> auto, ValidationChecks checks) throws CustomException, TypingException :
{
  	Token iS, eS, isES = null;
 	String p;
 	Transition t;
 	Boolean eC = false;
 	ASTNode postC = null;
 	Set <ASTVar> vars = null;
}
{
  	iS = < UNDERSCORE > <LBRACKET> <RBRACKET>
  		p = Participant() eC = ExternalCall() t = DeployAction(auto)
  			<LBRACKET> (postC = Assertion())? <RBRACKET>
  				<LBRACKET> (vars = VariableDeclaration())? <RBRACKET>
	  				eS = < IDWN > (isES = < PLUS >)?
	{
    	GrammarLogic.addTransition(auto, checks, t, iS, eS, isES, eC, null, postC, p, true, vars);
	}
}

Set <ASTVar> VariableDeclaration():
{
  	Token t1, t2;
  	Set <ASTVar> vars = new HashSet <ASTVar> ();
	IType ty1, ty2 = null;
}
{
	ty1 = Type() t1 = < IDWN > (<SEMICOLON> ty2 = Type() t2 = < IDWN > { vars.add(new ASTVar(t2.image, ty2)); })*
	{ vars.add(new ASTVar(t1.image, ty1)); return vars; }
}

void NormalTransition(Hashtable<String, Automaton> auto, ValidationChecks checks) throws CustomException, TypingException:
{ 
	Token iS, eS, isES = null;
 	String p;
 	Transition t;
 	Boolean eC = false;
 	ASTNode postC = null, preC = null;
}
{
	iS = < IDWN > <LBRACKET> (preC = Assertion())? <RBRACKET>
		p = ParticipantExtra() eC = ExternalCall() t = NormalAction(auto)
			<LBRACKET> (postC = Assertion())? <RBRACKET>
				eS = < IDWN > (isES = < PLUS >)?
	{
	 	GrammarLogic.addTransition(auto, checks, t, iS, eS, isES, eC, preC, postC, p, false, null);
	}
}

boolean ExternalCall():
{}
{
	<GT> { return false; }
	| <LT> { return true; }	  
}

String Participant():
{
	Token p1, p2 = null, p3 = null;
}
{
 	p1 = < IDWN > (p2 = <COLON> p3 = <IDWN>)?
 	{
		if(p2 == null) { return p1.image; }
		else { return p1.image.concat(p2.image).concat(p3.image); }
	}
}

String ParticipantExtra():
{
  	String p1, p2;
  	Token join;
  	Set < String > parts = new HashSet < String > ();
}
{
	p1 = Participant() (join = <MOREPART> p2 = Participant() { parts.add(join.image.concat(p2)); })*
	{ return p1.concat(String.join("", parts)); }
}


Transition DeployAction(Hashtable<String, Automaton> auto):
{
	Token c;
	List<Param> param = null;
}
{
  	< STARTLABEL > < LPAR > c = < IDWN > (<COMMA> param = ParamList())? < RPAR >
	{ return new Transition(c.image, "starts", ".", param); }
}

Transition NormalAction(Hashtable<String, Automaton> auto):
{
	Token c, aLabel;
 	String aType;
	List<Param> param;
}
{
	c = < IDWN > aType = OperationType() aLabel = < IDWN > < LPAR > param = ParamList() < RPAR >
	{ return new Transition(c.image, aLabel.image, aType, param); }
}


String OperationType():
{
  	Token aType;
}
{
	( aType = <MINUS>
	| aType = <DOT>)
	{ return aType.image; }	  
}

//	ASSERTIONS - PARAMETERS
List<Param> ParamList():
{
  	List<Param> l = new LinkedList<Param>();
  	Token x;
  	IType t;
}
{
  	// t = Type or ID -> check if its a state if it is not gives error
	[ t=TypeParam() x=<IDWN> { l.add(new Param(x.image, t));}
	( <COMMA> t=TypeParam() x=<IDWN> {l.add(new Param(x.image, t));} )*]
	{ return l; }
}

IType TypeParam():
{
  	IType t;
  	Token role;
}
{	
  	( role = <IDWN> { return new ParticipantType(role.image); }
    | t = Type() { return t; })
}

IType Type():
{
  	IType t;
  	Token role;
}
{	
  	( <INT> { return IntType.singleton; }
    | <BOOL> { return BoolType.singleton; }
	| <STRING> { return StringType.singleton; }
    | <SET> { return SetType.singleton; })
}

ASTNode Assertion():
{
	ASTNode n;
}
{
	n = Exp()
	{ return n; }
}

ASTNode Exp():
{
  	ASTNode n1, n2;
}
{
  	( n1 = BooleanExp() LogicalExp(n1)
  	| <NOT> n1 = Exp() { return new ASTNot(n1); }
 	) { return n1; }
}

ASTNode LogicalExp(ASTNode n1):
{
  	ASTNode n2;
}
{
  	(  	  <AND> n2 = Exp() { return new ASTAnd(n1, n2); }
 	| <OR> n2 = Exp() { return new ASTOr(n1, n2); }
	)?
	{ return n1; } 	
}

ASTNode BooleanExp():
{
  	ASTNode n1, n2, n3;
}
{
	( n1 = ArithmeticExp()
	 	( <EQ> n2 = ArithmeticExp() { return new ASTEq(n1, n2); }
	 	| <NEQ> n2 = ArithmeticExp() { return new ASTNeq(n1, n2); }
	 	| <LT> n2 = ArithmeticExp() { return new ASTLt(n1, n2); }
	 	| <GT> n2 = ArithmeticExp() { return new ASTGt(n1, n2); }
	 	| <LEQ> n2 = ArithmeticExp() { return new ASTLeq(n1, n2); }
	 	| <GEQ> n2 = ArithmeticExp() { return new ASTGeq(n1, n2); }
	 	| <VARASS> n2 = ArithmeticExp() { return new ASTAssign(n1, n2); }
		| <CONTAINS> n2 = ArithmeticExp() { return new ASTContains(n1, n2);})*
	{ return n1; } )
	| <EXISTS> n1 = AtomicExp() n2 = AtomicExp() n3 = Exp() { return new ASTExists(n1, n2, n3);}
	| <FORALL> n1 = AtomicExp() n2 = AtomicExp() n3 = Exp() { return new ASTForAll(n1, n2, n3);}
}

ASTNode ArithmeticExp() :
{ ASTNode n1, n2; }
{
   	n1 = UnaryExp() 
   	( <PLUS> n2 = UnaryExp() { return new ASTAdd(n1, n2); }
   	| <MINUS> n2 = UnaryExp() { return new ASTSub(n1, n2); }
   	| <TIMES> n2 = UnaryExp() { return new ASTMul(n1, n2); }
   	| <DIV> n2 = UnaryExp() { return new ASTDiv(n1, n2); })*
   	{ return n1; }
}

/*
	Atomic and negation
*/
ASTNode UnaryExp() :
{
 	ASTNode n1;
}
{
   	( n1 = AtomicExp() 
	| <MINUS> n1 = UnaryExp() { return new ASTSub(new ASTNum(0), n1); })
	{ return n1; }
}

ASTNode AtomicExp() :
{
	Token t1, t2;
	ASTNode n1;
	IType ty1 = null;
}
{
 	( t1 = <NUMBER> { return new ASTNum(Integer.parseInt(t1.image)); }
	| t1 = <STRING_LITERAL> { return new ASTString(t1.image); }
	| <SLPAR> <SRPAR> { return new ASTSet(); }
	| < TRUE > { return new ASTBool(true); }
	| < FALSE > { return new ASTBool(false); }
	|  LOOKAHEAD(2)(ty1 = Type())? t1 = < IDWN > {
		  if(ty1 == null)
		  	{ return new ASTId(t1.image); }
		  else
		  	{ return new ASTVar(t1.image, ty1); }
	})
	  	
}