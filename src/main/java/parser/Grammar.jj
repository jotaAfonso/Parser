/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */
options
{
 static = false;
}

PARSER_BEGIN(Parser)
package parser;
import data.*;
import java.util.Hashtable;
import java.util.HashSet;
import java.util.Set;
import validations.*;
import exceptions.CustomException;

public class Parser
{}

PARSER_END(Parser)

<*> SKIP :
{
	< " " | "\t" | "\r" | "\n" | "\r\n" >
	|
	<"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n") >
	|
	<"/*"(~["/"])*  "*""/" >
}

TOKEN :
{
 	< UNDERSCORE : "_" >
 	|
 	< PLUS : "+" >
 	|
 	< COLON : ":" >
	|
 	< NUMBER : [ "0"-"9" ] >
	|
	< LPAR : "(" >
  	|
  	< RPAR : ")" >
  	|
  	< NORMALCALL : ">" >
  	|
  	< EXTERNALCALL : "<" >
 	|
 	< MOREPART : "|" > 
  	|
	< STRING : (["A"-"Z","a"-"z"])+(["A"-"Z","a"-"z","0"-"9"])* >
	|
	< STARTLABEL : "starts(" >
	|
	< OPERATIONTYPE : ("-" | ".") >
}

int Start(Hashtable<String, Automaton> auto, Graph g, ValidationChecks checks) throws CustomException:
{}
{
  	DeployTransition(auto, g, checks)
	{
	  	return 0;
	}
 	| 
 	NormalTransition(auto, g)
	{
   	 	return 0;
   	}
   	|
   	<EOF>
   	{
   	 	return 1;
   	}
}

void DeployTransition(Hashtable<String, Automaton> auto, Graph g, ValidationChecks checks) throws CustomException:
{
  	Token iS;
 	Token eS;
 	String p;
 	Transition t;
 	Token isES = null;
 	Boolean eC = false;
 	Boolean rES = false;
}
{
  	iS = < UNDERSCORE >	p = ParticipantExtra() eC = ExternalCall() t = DeployAction(auto) eS = < STRING > (isES = < PLUS >)?
	{
	 	Automaton a = auto.get(t.getId());
	 	if (a == null) 
    		a = auto.put(t.getId(), new Automaton(t.getId()));
    	a = auto.get(t.getId());

		// transition
    	t.setFromS(iS.image);
    	if(isES != null)
    		rES = true;
    	t.setToS(eS.image);
    	t.addParticipant(p);
    	t.setExternalCall(eC);

		// automaton
		a.addBothStates(iS.image, eS.image, rES);
    	a.addTransition(t);
    	a.addRoleParticipant(p);

    	// graph
    	g.addEdge(iS.image, eS.image, p);

    	// checks
    	checks.getContractsRegistered().add(t.getId());
	}
}

void NormalTransition(Hashtable<String, Automaton> auto, Graph g) throws CustomException:
{ 
	Token iS;
 	Token eS;
 	String p;
 	Transition t;
 	Token isES = null;
 	Boolean eC = false;
 	Boolean rES = false;
}
{
	iS = < STRING > p = ParticipantExtra() eC = ExternalCall() t = NormalAction(auto) eS = < STRING > (isES = < PLUS >)?
	{
	 	Automaton a = auto.get(t.getId());
	 	if (a == null) 
    		a = auto.put(t.getId(), new Automaton(t.getId()));
    	a = auto.get(t.getId());

		// transition
    	t.setFromS(iS.image);
    	if(isES != null)
    		rES = true;
    	t.setToS(eS.image);
    	t.addParticipant(p);
    	t.setExternalCall(eC);

		// automaton
		a.addBothStates(iS.image, eS.image, rES);
    	a.addTransition(t);
    	a.addRoleParticipant(p);

		// graph
    	g.addEdge(iS.image, eS.image, p);
	}
}

boolean ExternalCall():
{
}
{
	<NORMALCALL> { return false; }
	|
	<EXTERNALCALL> { return true; }	  
}

String Participant():
{
	Token p1;
	Token p2 = null;
	Token p3 = null;
}
{
 	p1 = < STRING > (p2 = <COLON> p3 = <STRING>)?
 	{
		if(p2 == null)
	 		return p1.image;
		else
			return p1.image.concat(p2.image).concat(p3.image);
	}
}

String ParticipantExtra():
{
  	String p1;
  	String p2;
  	Token join;
  	Set < String > parts = new HashSet < String > ();
}
{
	p1 = Participant() (join = <MOREPART> p2 = Participant() { parts.add(join.image.concat(p2)); })*
	{
		String concat = String.join("", parts);
		return p1.concat(concat);
	}
}


Transition DeployAction(Hashtable<String, Automaton> auto):
{
	Token c;
}
{
  	< STARTLABEL > c = < STRING > < RPAR >
	{
	  	return new Transition(c.image, "starts", ".");
	}
}

Transition NormalAction(Hashtable<String, Automaton> auto):
{
	Token c;
 	Token aType;
 	Token aLabel;
}
{
	c = < STRING > aType = < OPERATIONTYPE > aLabel = < STRING > < LPAR > < RPAR >
	{
		return new Transition(c.image, aLabel.image, aType.image);
	}
}